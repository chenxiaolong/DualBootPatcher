buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    jcenter()
    google()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    packagingOptions {
        // CMake will strip the libraries automatically for release builds
        doNotStrip '**.so'
    }

    lintOptions {
        disable 'ExtraTranslation'
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "com.github.chenxiaolong.dualbootpatcher"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 1
        versionName "@MBP_VERSION@"
        buildConfigField "String", "GIT_SHA", '"@_GIT_SHA@"'
    }

    signingConfigs {
        release

        debug

        ci
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_release"

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_debug"

            // Uncomment to use proguard for debug build
            //shrinkResources true
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        ci {
            signingConfig signingConfigs.ci
            applicationIdSuffix ".snapshot"
            versionNameSuffix "-SNAPSHOT"
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_snapshot"

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            matchingFallbacks = ['release']
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', '../external/flatbuffers/java']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = [
                '@CMAKE_BINARY_DIR@/assets/',
                'assets'
            ]
            jniLibs.srcDirs = [
                '@CMAKE_BINARY_DIR@/android/result/lib/',
            ]
        }

        debug {
            manifest.srcFile 'manifests/debug.xml'
        }

        ci {
            manifest.srcFile 'manifests/ci.xml'
        }
    }
}

def readSigningConfig(path, target) {
    def props = new Properties()
    def propFile = new File(path)
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile));
        if (props != null
                && props.containsKey('keystore')
                && props.containsKey('keystore_passphrase')
                && props.containsKey('key_alias')
                && props.containsKey('key_passphrase')) {
            target.storeFile = file(props['keystore'])
            target.storePassword = props['keystore_passphrase']
            target.keyAlias = props['key_alias']
            target.keyPassword = props['key_passphrase']
            return true;
        }
    }
    return false;
}

readSigningConfig('@_RELEASE_SIGN_CONFIG_PATH@', android.signingConfigs.release)
readSigningConfig('@_DEBUG_SIGN_CONFIG_PATH@', android.signingConfigs.debug)
readSigningConfig('@_CI_SIGN_CONFIG_PATH@', android.signingConfigs.ci)

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "android.arch.lifecycle:extensions:1.1.1"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"

    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:palette-v7:27.1.1'
    implementation 'com.android.support:preference-v7:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:support-v13:27.1.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'org.apache.commons:commons-text:1.3'
    implementation 'net.java.dev.jna:jna:4.5.1@aar'
    implementation project(":emulatorview")
    implementation project(":rootshell")

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    ciImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}
