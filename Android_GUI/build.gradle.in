buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    packagingOptions {
        // Both commons-lang3 and common-io want to include these file
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        disable 'ExtraTranslation'
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "com.github.chenxiaolong.dualbootpatcher"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 1
        versionName "@MBP_VERSION@"
    }

    signingConfigs {
        release {
            storeFile file("/dev/null")
            keyAlias ""
            storePassword ""
            keyPassword ""
        }

        debug
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_release"
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_debug"
        }

        ci {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".snapshot"
            versionNameSuffix "-SNAPSHOT"
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_snapshot"
        }
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleRelease)) {
            def propSigning = new File('signing.properties')

            if (propSigning.canRead()) {
                def Properties props = new Properties()
                props.load(new FileInputStream(propSigning))

                if (props != null && props.containsKey('storeFile')) {
                    signingConfigs.release.storeFile = file(props['storeFile'])
                    signingConfigs.release.storePassword = new String(System.console().readPassword("Enter keystore password: "))
                    signingConfigs.release.keyAlias = System.console().readLine("Enter alias key: ")
                    signingConfigs.release.keyPassword = new String(System.console().readPassword("Enter key password: "))
                }
                else {
                    buildTypes.release.signingConfig = null
                }
            }
            else {
                buildTypes.release.signingConfig = null
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', '../external/flatbuffers/java']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = [
                '@CMAKE_BINARY_DIR@/assets/',
                'assets'
            ]
            jniLibs.srcDirs = [
                '@CMAKE_BINARY_DIR@/libmbp/out/',
                '@CMAKE_BINARY_DIR@/libmiscstuff/out/',
                '@CMAKE_BINARY_DIR@/thirdparty/android/jna/@JNA_VER@/all/lib/'
            ]
        }

        debug {
            manifest.srcFile 'manifests/debug.xml'
        }

        ci {
            manifest.srcFile 'manifests/ci.xml'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:support-v13:23.1.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.nhaarman.listviewanimations:lib-core:3.1.0@aar'
    compile 'com.nhaarman.listviewanimations:lib-manipulation:3.1.0@aar'
    compile 'com.nhaarman.listviewanimations:lib-core-slh:3.1.0@aar'
    compile('com.afollestad.material-dialogs:core:0.8.0.1@aar') {
        transitive = true
    }
    compile('com.afollestad.material-dialogs:commons:0.8.0.1@aar') {
        transitive = true
    }
    compile 'com.getbase:floatingactionbutton:1.10.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'
    compile project(":emulatorview")
    compile files('@CMAKE_BINARY_DIR@/thirdparty/android/jna/@JNA_VER@/all/jna.jar')
    compile files('libs/RootTools-4.1.jar')

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    ciCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
